plugins {
    id 'java'
    id 'de.fuerstenau.buildconfig' version "${build_config_version}"
}

group = project.group
version = project.version

println(' Java: ' + System.getProperty('java.version') +
        ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')' +
        ' Arch: ' + System.getProperty('os.arch'))

buildConfig {
    buildConfigField "String", "HASH", "${execute("git rev-parse --short HEAD")}"
    buildConfigField "String", "BRANCH", "${execute("git branch --show-current")}"
    buildConfigField "String", "VERSION", "${project.version}"
    buildConfigField "int", "BUILD", "${project.build}"

    packageName "me.graza.tbot"
}

sourceSets.main.java.srcDirs += "build/gen/buildconfig/src/main"

repositories {
    mavenCentral()
}

configurations {
    compile
    implementation.extendsFrom compile
}

dependencies {
    compile group: 'org.javacord', name: 'javacord', version: javacord_version
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logback_classic_version
    compile group: 'org.openjdk.nashorn', name: 'nashorn-core', version: nashorn_version
    compile group: 'org.mongodb', name: 'mongodb-driver-sync', version: mongodb_driver_version
    compile group: 'org.json', name: 'json', version: json_version
}

def execute(String cmd) {
    try {
        def stdout = new ByteArrayOutputStream()

        exec {
            commandLine cmd.split(" ")
            standardOutput = stdout
        }

        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}
